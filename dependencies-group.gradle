def Group(Closure closure) {
    closure.delegate = dependencies
    return closure
}

ext {

    //-------------------------------------------------------------------||
    // LIBRARIES                                                         ||
    //-------------------------------------------------------------------||

    libraries = Group {
        implementation deps.libs
    }

    //-------------------------------------------------------------------||
    // KOTLIN                                                            ||
    //-------------------------------------------------------------------||

    kotlin = Group {
        implementation deps.kotlin.lib
        implementation deps.kotlin.ktx

    }

    //-------------------------------------------------------------------||
    // NETWORKING                                                        ||
    //-------------------------------------------------------------------||

    networking = Group {
        implementation deps.retrofit.retrofit
        implementation deps.retrofit.gson
        implementation deps.retrofit.rx
        implementation deps.okHttp.logging
        implementation deps.signpost.signpostCore
        implementation deps.signpost.akerfeldt
    }

    //-------------------------------------------------------------------||
    // RX                                                                ||
    //-------------------------------------------------------------------||

    rx = Group {
        implementation deps.rx.android
        implementation deps.rx.java
    }

    //-------------------------------------------------------------------||
    // ARCHITECTURE COMPONENT                                            ||
    //-------------------------------------------------------------------||

    architecture = Group {
        implementation deps.ac.extensions
        implementation deps.ac.viewModel
        implementation deps.ac.liveData
        implementation deps.ac.paging
        implementation deps.ac.room
        implementation deps.ac.roomRx2


        kapt deps.ac.roomCompiler
        testImplementation deps.ac.testing
    }

    //-------------------------------------------------------------------||
    // VIEW                                                              ||
    //-------------------------------------------------------------------||

    view = Group {
        implementation deps.support.app
        implementation deps.support.cardView
        implementation deps.support.recyclerView
        implementation deps.support.constraint
        implementation deps.support.design
        implementation deps.glide
        implementation (deps.yalantis){ exclude module: 'support-v4' }
        implementation (deps.animatedPullToRefresh) { exclude module: 'support-v4' }
        implementation (deps.davemorrissey){ exclude module: 'support-v4' }
        implementation deps.toro3.toro
        implementation deps.toro3.toroExtExoplayer

    }

    //-------------------------------------------------------------------||
    // DI                                                              ||
    //-------------------------------------------------------------------||

    di = Group {
        implementation deps.dagger.dagger
        implementation deps.dagger.android

        kapt deps.dagger.compiler
        kapt deps.dagger.processor

    }

    //-------------------------------------------------------------------||
    // TESTING                                                           ||
    //-------------------------------------------------------------------||

    testing = Group {
        testImplementation deps.jUnit
        testImplementation deps.mockito.core
        testImplementation deps.mockito.inline
        testImplementation deps.mockito.kotlin

        androidTestImplementation deps.supportTest.runner
        androidTestImplementation deps.supportTest.rule
        androidTestImplementation deps.supportTest.espresso
        androidTestImplementation deps.mockito.mockitoAndroid
        androidTestImplementation deps.mockito.kotlin

        kaptTest deps.dagger.compiler
        kaptAndroidTest deps.dagger.compiler
    }

}